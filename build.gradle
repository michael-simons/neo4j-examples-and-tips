plugins {
    id "org.asciidoctor.convert" version "2.4.0"
    id "com.github.jruby-gradle.base" version "2.0.1"
    id 'org.kordamp.gradle.livereload' version '0.4.0'
}


repositories {
    ruby.gems()
}

group = "org.neo4j.tips"

dependencies {
    asciidoctor 'org.asciidoctor:asciidoctorj-pdf:1.6.2'
    gems 'rubygems:asciidoctor-diagram:1.5.19'
}

import org.asciidoctor.gradle.AsciidoctorTask

tasks.withType(AsciidoctorTask) { docTask ->
    dependsOn jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir

    attributes \
          'examples-gh-ref': 'https://github.com/michael-simons/neo4j-sdn-ogm-tips/tree/master/examples',
          'examples-gh-ref-blob': 'https://github.com/michael-simons/neo4j-sdn-ogm-tips/blob/master/examples',
          'published-ref': 'https://michael-simons.github.io/neo4j-sdn-ogm-tipse',
          'examples': "$projectDir/examples",
          'allow-uri-read': true,
          'imagesdir': 'images'

    sourceDir = new File("src/main/asciidoc")
    outputDir = project.buildDir

    resources {
        from(sourceDir) {
            include '**/*.png', '**/*.gif'
        }
    }
}

task generateHTML (
        type: AsciidoctorTask,
        description: 'use html5 as asciidoc backend'
) {
    backends = ['html5']
}

liveReload {
    docRoot asciidoctor.outputDir.canonicalPath + '/html5'
}

defaultTasks "generateHTML"
